PROJECT(MARATIS)

## Let's use a reasonable modern version
CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
SET(CMAKE_HAS_SORT 1)
SET(CMAKE_MODULE_PATH ${MARATIS_SOURCE_DIR}/CMake ${CMAKE_MODULE_PATH})

## Disallow in-source builds
INCLUDE(OutOfSourceBuild)

## Documentation tool
FIND_PACKAGE(Doxygen 1.8.3)

## Set version and info
SET(MARATIS_MAJOR_VERSION 3)
SET(MARATIS_MINOR_VERSION 21)
SET(MARATIS_PATCH_VERSION 0)

SET(MARATIS_PACKAGE "Maratis")
SET(MARATIS_VENDOR "Anael Seghezzi")
SET(MARATIS_VERSION "${MARATIS_MAJOR_VERSION}.${MARATIS_MINOR_VERSION}.${MARATIS_PATCH_VERSION}")
SET(MARATIS_COPYRIGHT "Copyright (c) 2003-2014, ${MARATIS_VENDOR}.")

MESSAGE(STATUS "${MARATIS_PACKAGE} ${MARATIS_VERSION}")

## Packaging
SET(CPACK_PACKAGE_NAME "${MARATIS_PACKAGE}")
SET(CPACK_PACKAGE_VENDOR "${MARATIS_VENDOR}")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Maratis editor and game engine.")
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
SET(CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
SET(CPACK_PACKAGE_ICON "${CMAKE_CURRENT_SOURCE_DIR}/Data/Installer/maratis-icon-55x55.png")
SET(CPACK_PACKAGE_VERSION_MAJOR "${MARATIS_MAJOR_VERSION}")
SET(CPACK_PACKAGE_VERSION_MINOR "${MARATIS_MINOR_VERSION}")
SET(CPACK_PACKAGE_VERSION_PATCH "${MARATIS_PATCH_VERSION}")
INCLUDE(CPack)

## Set install path
SET(MARATIS_INSTALL_DIR Maratis)

## Set build flags
IF(WIN32)
  SET(CMAKE_C_FLAGS "/EHsc /nologo /GR /FC /DWIN32 /D_MBCS")
  SET(CMAKE_C_FLAGS_DEBUG "/Z7 /W4 /Od /MDd /D_DEBUG /D__Debug__")
  SET(CMAKE_C_FLAGS_RELEASE "/W1 /O2 /Ot /MD /DNDEBUG")
  SET(CMAKE_CXX_FLAGS "/EHsc /nologo /GR /FC /DWIN32 /D_MBCS")
  SET(CMAKE_CXX_FLAGS_DEBUG "/Z7 /W4 /Od /MDd /D_DEBUG /D__Debug__")
  SET(CMAKE_CXX_FLAGS_RELEASE "/W1 /O2 /Ot /MD /DNDEBUG")
ENDIF(WIN32)
IF(APPLE)
  SET(CMAKE_C_FLAGS "-isysroot /Developer/SDKs/MacOSX10.6.sdk -DOSX")
  SET(CMAKE_C_FLAGS_DEBUG "-g -D__Debug__")
  SET(CMAKE_CXX_FLAGS "-isysroot /Developer/SDKs/MacOSX10.6.sdk -DOSX")
  SET(CMAKE_CXX_FLAGS_DEBUG "-g -D__Debug__")
  SET(CMAKE_SHARED_LINKER_FLAGS "-Wl")
ENDIF(APPLE)
IF(UNIX AND NOT APPLE)
  OPTION(MARATIS_BUILD_32BIT "Force 32bit build" ON)
  IF(MARATIS_BUILD_32BIT)
    SET(MARATIS_BUILD_FLAGS "-m32")
  ENDIF(MARATIS_BUILD_32BIT)
  SET(CMAKE_C_FLAGS "${MARATIS_BUILD_FLAGS} -O2 -fPIC -DLINUX")
  SET(CMAKE_C_FLAGS_DEBUG "-g -D__Debug__")
  SET(CMAKE_CXX_FLAGS "${MARATIS_BUILD_FLAGS} -O2 -fPIC -DLINUX")
  SET(CMAKE_CXX_FLAGS_DEBUG "-g -D__Debug__")
  SET(CMAKE_SHARED_LINKER_FLAGS "${MARATIS_BUILD_FLAGS} -Wl,--as-needed -Wl,--allow-multiple-definition")
ENDIF(UNIX AND NOT APPLE)

## Set up system dependencies
FIND_PACKAGE(OpenGL REQUIRED)

## Set up 3rd party dependencies
SET(ASSIMP_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/asimp/include)
SET(BULLET_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/bullet)
SET(DEVIL_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/devil)
SET(DIRENT_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/dirent/include)
SET(FREETYPE_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/freetype/include)
SET(GLEE_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/glee)
SET(JPEG_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/libjpeg)
SET(PNG_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/libpng)
SET(SNDFILE_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/libsndfile/include)
SET(LUA_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/lua)
SET(NPK_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/npk/include)
SET(OPENAL_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/openal/include)
SET(TINYXML_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/tinyxml)
SET(ZLIB_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/3rdparty/zlib)

SET(ASSIMP_LIBRARIES assimp)
SET(BULLET_LIBRARIES bullet)
SET(DEVIL_LIBRARIES devil)
SET(FREETYPE_LIBRARIES freetype)
SET(GLEE_LIBRARIES glee)
SET(JPEG_LIBRARIES jpeg)
SET(PNG_LIBRARIES png)
IF(APPLE)
  SET(SNDFILE_LIBRARIES SndFile)
ELSE(APPLE)
  SET(SNDFILE_LIBRARIES sndfile)
ENDIF(APPLE)
SET(LUA_LIBRARIES lua)
SET(NPK_LIBRARIES npk)
SET(TINYXML_LIBRARIES tinyxml)
IF(WIN32)
  SET(OPENAL_LIBRARIES OpenAL32)
ELSE(WIN32)
  SET(OPENAL_LIBRARIES openal)
ENDIF(WIN32)
SET(ZLIB_LIBRARIES zlib)

## Set up SDK dependencies
SET(MCORE_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/MSDK/MCore/Includes)
SET(MENGINE_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/MSDK/MEngine/Includes)
SET(MGUI_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/MSDK/MGui/Includes)

SET(MCORE_LIBRARIES MCore)
SET(MENGINE_LIBRARIES MEngine)
SET(MGUI_LIBRARIES MGui)

## Set up Maratis dependencies
SET(MARATIS_COMMON_INCLUDE_DIR ${MARATIS_SOURCE_DIR}/Maratis/Common)
SET(MARATIS_COMMON_LIBRARIES MaratisCommon)

## Set up subdirectories...
ADD_SUBDIRECTORY(3rdparty)
ADD_SUBDIRECTORY(MSDK)
ADD_SUBDIRECTORY(Maratis)
ADD_SUBDIRECTORY(Data)
ADD_SUBDIRECTORY(Documentation)

## Install files
INSTALL(FILES 
  CREDITS
  LICENSE
  README.md
  DESTINATION ${MARATIS_INSTALL_DIR})
